%%  comment symbol
DATATYPE STACKEC;
%%  AUTHOR Reinhard Buendgen
%%  STATUS completed
%%  DESCRIPTION stack (push,pop,top) with error handling
SORT
	EL, STACK;
CONST
	0, 1, ERREL: EL;
	NIL, ERROR: STACK;
VAR
	A, B, C: EL;
	R, S, T: STACK;
OPERATOR
	PUSH: EL, STACK -> STACK;
	POP: STACK -> STACK;
	TOP: STACK -> EL;
	GOODSTACK: STACK -> STACK;
	IF: STACK, EL, EL -> EL;	%%  NIL = true, ERROR = false
NOTATION
	PUSH, POP, TOP, GOODSTACK: FUNCTION;
AXIOM
  [1] PUSH(A,ERROR)==ERROR;
  [2] PUSH(ERREL,S)==ERROR;
  [3] POP(NIL)==ERROR;
  [4] POP(ERROR)==ERROR;
  [5] POP(PUSH(0,S))==S;
  [6] POP(PUSH(1,S))==S;
  [7] GOODSTACK(ERROR)==ERROR;
  [8] GOODSTACK(NIL)==NIL;
  [9] GOODSTACK(PUSH(0,S))==GOODSTACK(S);
 [10] GOODSTACK(PUSH(1,S))==GOODSTACK(S);
 [11] IF(NIL,A,B)==A;
 [12] IF(ERROR,A,B)==B;
 [13] IF(PUSH(A,S),B,C)==IF(GOODSTACK(PUSH(A,S)),B,C);
 [14] IF(GOODSTACK(S),ERREL,ERREL) == ERREL;
 [15] TOP(ERROR)==ERREL;
 [16] TOP(NIL)==ERREL;
 [17] TOP(PUSH(A,S))==IF(GOODSTACK(S),A,ERREL);
END
