%% 
DATATYPE JP4;
%%  ATHOUR H. R. Walters & H. Zantema
%%  ORIGIN "Rewrite systems for integer arithmetic" (LNCS 914), 1995
SORT
	Int;
CONST
	0, 1, 2, 3: Int;
VAR
	x, y, z: Int;
OPERATOR
	[: Int, Int -> Int;
	~: Int -> Int;
	+: Int, Int -> Int;
	*: Int, Int -> Int;
	M: Int, Int -> Int;
NOTATION
	~, M: FUNCTION;
	+, *: INFIX;
	[: ROUNDFIX ];
AXIOM
     [1] [ 0, x ] == x;
     [2] [ x, [ y,z ] ] == [ (x + y),z ];
     [3] [ x, ~([ y,z ]) ] == ~([ M(y,x),z ]);
   [401] [ 1, ~(1) ] == [ 0,3 ];
   [402] [ 1, ~(2) ] == [ 0,2 ];
   [403] [ 1, ~(3) ] == [ 0,1 ];
   [404] [ 2, ~(1) ] == [ 1,3 ];
   [405] [ 2, ~(2) ] == [ 1,2 ];
   [406] [ 2, ~(3) ] == [ 1,1 ];
   [407] [ 3, ~(1) ] == [ 2,3 ];
   [408] [ 3, ~(2) ] == [ 2,2 ];
   [409] [ 3, ~(3) ] == [ 2,1 ];
   [501] [ [ x,0 ],~(1) ] == [ [ x,~(1) ],3 ];
   [502] [ [ x,0 ],~(2) ] == [ [ x,~(1) ],2 ];
   [502] [ [ x,0 ],~(3) ] == [ [ x,~(1) ],1 ];
   [601] [ [ x,1 ], ~(1) ] == [ [ x,0 ], 3 ];
   [602] [ [ x,1 ], ~(2) ] == [ [ x,0 ], 2 ];
   [603] [ [ x,1 ], ~(3) ] == [ [ x,0 ], 1 ];
   [604] [ [ x,2 ], ~(1) ] == [ [ x,1 ], 3 ];
   [605] [ [ x,2 ], ~(2) ] == [ [ x,1 ], 2 ];
   [606] [ [ x,2 ], ~(3) ] == [ [ x,1 ], 1 ];
   [607] [ [ x,3 ], ~(1) ] == [ [ x,2 ], 3 ];
   [608] [ [ x,3 ], ~(2) ] == [ [ x,2 ], 2 ];
   [609] [ [ x,3 ], ~(3) ] == [ [ x,2 ], 1 ];
     [7] [ ~(x),y ] == ~([ x,~(y) ]);
     [8] ~(~(x)) == x;
     [9] ~(0) == 0;
    [10] (0 + x) == x;
    [11] (x + 0) == x;
  [1201] (1 + 1) == 2;
  [1202] (1 + 2) == 3;
  [1203] (1 + 3) == [ 1,0 ];
  [1204] (2 + 1) == 3;
  [1205] (2 + 2) == [ 1,0 ];
  [1206] (2 + 3) == [ 1,1 ];
  [1207] (3 + 1) == [ 1,0 ];
  [1208] (3 + 2) == [ 1,1 ];
  [1209] (3 + 3) == [ 1,2 ];
    [13] (x + [ y,z ]) == [ y,(x + z) ];
    [14] ([ x,y ] + z) == [ x,(y + z) ];
    [15] (x + ~(y)) == M(x,y);
    [16] (~(x) + y) == M(y,x);
    [17] M(0,x) == ~(x);
    [18] M(x,0) == x;
  [1900] M(x,x) == 0;
  [1902] M(1,2) == ~(1);
  [1903] M(1,3) == ~(2);
  [1904] M(2,1) == 1;
  [1906] M(2,3) == ~(1);
  [1907] M(3,1) == 2;
  [1908] M(3,2) == 1;
    [20] M([ x,y ],z) == [ x,M(y,z) ];
    [21] M(x,[ y,z ]) == ~([ y,M(z,x) ]);
    [22] M(x,~(y)) == (x + y);
    [23] M(~(x),y) == ~((x + y));
    [24] (0 * x) == 0;
    [25] (x * 0) == 0;
  [2600] (1 * x) == x;
  [2601] (x * 1) == x;
  [2605] (2 * 2) == [ 1,0 ];
  [2606] (2 * 3) == [ 1,2 ];
  [2608] (3 * 2) == [ 1,2 ];
  [2609] (3 * 3) == [ 2,1 ];
    [27] (x * [ y,z ]) == [ (x * y),(x * z) ];
    [28] ([ x,y ] * z) == [ (x * z),(y * z) ];
    [29] (x * ~(y)) == ~((x * y));
    [30] (~(x) * y) == ~((x * y));
END
