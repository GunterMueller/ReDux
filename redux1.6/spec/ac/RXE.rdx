DATATYPE RX;
SORT
	KOEF, POLYNOM, UNBEK;
CONST
	0, 1: KOEF;
	I, X, Y, Z: UNBEK;
	O: POLYNOM;
VAR
	a, b, c, d, e: KOEF;
	x, y, z, w: UNBEK;
	p, q, r, s, t: POLYNOM;
OPERATOR
	+: KOEF, KOEF -> KOEF;
	*: KOEF, KOEF -> KOEF;
	~: KOEF -> KOEF;
	|: UNBEK, UNBEK -> UNBEK;
	M: KOEF, UNBEK -> POLYNOM;
	#: POLYNOM, POLYNOM -> POLYNOM;
	@: POLYNOM, POLYNOM -> POLYNOM;
NOTATION
	+, *, ~, |, M, #, @: FUNCTION;
THEORY
	+, *, |, #, @: AC;
AXIOM
[1] +(0,a) == a;
[2] *(0,a) == 0;
[3] *(1,a) == a;
[4] ~(0) == 0;
[5] ~(~(a)) == a;
[6] +(a,~(a)) == 0;
[7] ~(+(a,b)) == +(~(a),~(b));
[8] *(a,~(b)) == ~(*(a,b));
[9] *(a,+(b,c)) == +(*(a,b),*(a,c));

[10] |(x,I) == x;

[11] #(M(a,x),M(b,x)) == M(+(a,b),x);
[12] @(M(a,x),M(b,y)) == M(*(a,b),|(x,y));

[13] #(p,O) == p;
[14] @(p,M(1,I)) == p;
[15] @(p,#(q,r)) == #(@(p,q),@(p,r));
[16] #(p,@(p,M(~(1),I))) == O;
END
; This file is RXE.typ 
