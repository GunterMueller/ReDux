%%  comment symbol
DATATYPE RNG;
SORT
	RNG;
CONST
	0, 1: RNG;
	A11, A12, A21, A22, B11, B12, B21, B22: RNG;
VAR
	x, y, z, X, Y, Z: RNG;
OPERATOR
	C11:  -> RNG;
	C12:  -> RNG;
	C21:  -> RNG;
	C22:  -> RNG;
	c11:  -> RNG;
	c12:  -> RNG;
	c21:  -> RNG;
	c22:  -> RNG;
	S1:  -> RNG;
	S2:  -> RNG;
	S3:  -> RNG;
	S4:  -> RNG;
	S5:  -> RNG;
	S6:  -> RNG;
	S7:  -> RNG;
	S8:  -> RNG;
	M1:  -> RNG;
	M2:  -> RNG;
	M3:  -> RNG;
	M4:  -> RNG;
	M5:  -> RNG;
	M6:  -> RNG;
	M7:  -> RNG;
	T1:  -> RNG;
	T2:  -> RNG;
	+: RNG, RNG -> RNG;
	*: RNG, RNG -> RNG;
	~: RNG -> RNG;
NOTATION
	~: FUNCTION;
	+, *: INFIX;
THEORY
	+: AC;
AXIOM
[1] (0 + x) == x;
[2] (~(x) + x) == 0;
[3] (1 * x) == x;
[4] (x * 1) == x;
[5] (x * (y + z)) == ((x * y) + (x * z));
[6] ((x + y) * z) == ((x * z) + (y * z));
[7] ((x * y) * z) == (x * (y * z));
 [8] ~(~(x)) == x;
 [9] ~(0) == 0;
[10] ~((x + y)) == (~(y) + ~(x));
[11] (x * 0) == 0;
[12] (0 * z) == 0;
[13] (y * ~(x)) == ~((y * x));
[14] (~(x) * z) == ~((x * z));

 %%  klassisch
[15] C11 == ((A11 * B11) + (A12 * B21));
[16] C12 == ((A11 * B12) + (A12 * B22));
[17] C21 == ((A21 * B11) + (A22 * B21));
[18] C22 == ((A21 * B12) + (A22 * B22));

 %%  Strassen / Winograd

[19] c11 == ( M2 + M3 );
[20] c12 == ( T1 + M5 + M6 );
[21] c21 == ( T2 + ~(M7) );
[22] c22 == ( T2 + M5 );
[23] S1 == ( A21 + A22 );
[24] S2 == ( S1 + ~(A11) );
[25] S3 == ( A11 + ~(A21) );
[26] S4 == ( A12 + ~(S2) );
[27] S5 == ( B12 + ~(B11) );
[28] S6 == ( B22 + ~(S5) );
[29] S7 == ( B22 + ~(B12) );
[30] S8 == ( S6 + ~(B21) );
[31] M1 == ( S2 * S6 );
[32] M2 == ( A11 * B11 );
[33] M3 == ( A12 * B21 );
[34] M4 == ( S3 * S7 );
[35] M5 == ( S1 * S5 );
[36] M6 == ( S4 * B22 );
[37] M7 == ( A22 * S8 );
[38] T1 == ( M1 + M2 );
[39] T2 == ( T1 + M4 );
END
%%  This file is aring1.typ 
