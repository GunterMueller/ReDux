%%   
DATATYPE NAT;
%%  AUTHOR Reinhard Buendgen
%%  STATUS divergent
%%  DESCRIPTION natural numbers: exp & mod 3
%%         rules 1) - 10) are canonical
%%         with axioms 11 and 12 the theorem
%%         mod3(^(s(s(0)),+(a,a))) == s(0)
%%         can be proved inductively.
%%         if axioms 11 and 12 are missing this is not possible
SORT
	NAT;
CONST
	0: NAT;
VAR
	a, b, c, d, e: NAT;
OPERATOR
	s: NAT -> NAT;
	+: NAT, NAT -> NAT;
	*: NAT, NAT -> NAT;
	^: NAT, NAT -> NAT;
	mod3: NAT -> NAT;
NOTATION
	s, +, *, ^, mod3: FUNCTION;
AXIOM
[1] +(0,a)==a;
[2] +(s(b),a)==s(+(b,a));
[3] *(0,a)==0;
[4] *(s(a),b)==+(*(a,b),b);
[5] ^(a,0) == s(0);
[6] ^(a,s(b)) == *(a,^(a,b));
[7] mod3(0)==0;
[8] mod3(s(0))==s(0);
[9] mod3(s(s(0)))==s(s(0));
[10] mod3(s(s(s(a)))) == mod3(a);
[11] mod3(+(a,+(a,a))) == 0;
[12] mod3(+(a,+(a,+(a,b)))) == mod3(b);
END
