%% 
DATATYPE XNATSUM;
%%  DESCRIPTION Spezifikation der natuerlichen Zahlen mit 
%%  DESCRIPTION simulierter externer Sorte, so dass ein
%%  DESCRIPTION kanonisches XTRS existiert, zusammen mit
%%  DESCRIPTION definiertem operator SUM
SORT
	EXTERN, NAT;
CONST
	0, 1: EXTERN;
VAR
	a, b, c, d, e: NAT;
	A, B, C: EXTERN;
OPERATOR
	o:  -> NAT;
	i:  -> NAT;
	+: NAT, NAT -> NAT;
	*: NAT, NAT -> NAT;
%%  coercion operator
	": EXTERN -> NAT;
%%  external operators
	#: EXTERN, EXTERN -> EXTERN;
	@: EXTERN, EXTERN -> EXTERN;
%% defined operater
	SUM: NAT -> NAT;
NOTATION
	SUM: FUNCTION;
	+, *, #, @: INFIX;
	": ROUNDFIX ";
THEORY
	+, *, #, @: AC;
AXIOM
  [1] (o + a) == a;
  [2] (o * a) == o;
  [3] (i * a) == a;
  [4] (a * (b + c)) == ((a * b) + (a * c));

  [5] (0 # A) == A;
  [6] (0 @ A) == 0;
  [7] (1 @ A) == A;
  [8] (A @ (B # C)) == ((A @ B) # (A @ C));

  [9] o == " 0 ";
  [10] i == " 1 ";
  [11] (" A " + " B ") == " (A # B) ";
  [12] (" A " * " B ") == " (A @ B) ";

  [13] SUM(o) == o;
  [14] SUM((a + i)) == (a + i + SUM(a));
END
