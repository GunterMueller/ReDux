%%  comment symbol
DATATYPE RXC;
%%  AUTHOR Reinhard Buendgen
%%  ORIGIN Reinhard Buendgen: Term Completion versus Algebraic Completion
%%  ORIGIN PhD-thesis, 1991, Spec. 5.2
%%  DESCRIPTION 3-variate polynomials (distributive representation)
%%  STATUS complete
SORT
	KOEF, POLYNOM, UNBEK;
CONST
	0, 1: KOEF;
	I, X, Y, Z: UNBEK;
	O: POLYNOM;
VAR
	a, b, c, d, e: KOEF;
	x, y, z, w: UNBEK;
	p, q, r, s, t: POLYNOM;
OPERATOR
	+: KOEF, KOEF -> KOEF;
	*: KOEF, KOEF -> KOEF;
	-: KOEF -> KOEF;
	.: UNBEK, UNBEK -> UNBEK;
	M: KOEF, UNBEK -> POLYNOM;
	#: POLYNOM, POLYNOM -> POLYNOM;
	@: POLYNOM, POLYNOM -> POLYNOM;
NOTATION
	M: FUNCTION;
PREC
        + < *;
        # < @;
THEORY
	+, *, ., #, @: AC;
AXIOM
  [1] 0 + a == a;
  [2] 0 * a == 0;
  [3] 1 * a == a;
  [4] -0 == 0;
  [5] --a == a;
  [6] (a + -a) == 0;
  [7] -(a + b) == -a + -b;
  [8] a * -b == -(a * b);
  [9] a * (b + c) == a * b + a * c;
  
 [10] x . I == x;
 
 [11] M(0,x) == O;
 [12] M(a,x) # M(b,x) == M(a + b,x);
 [13] M(a,x) @ M(b,y) == M(a * b,x . y);
 
 [14] p # O == p;
 [15] p @ O == O;
 [16] p @ M(1,I) == p;
 [17] p @ (q # r) == p @ q # p @ r;
 [18] p @ M(a,x) # p @ M(b,x) == p @ M(a + b,x);
 [19] p @ M(1 + b,I) == p # p @ M(b,I);
 [20] p # p @ M(-1,I) == O;
 [21] p # p @ M(-1 + a,I) == p @ M(a,I);
END

