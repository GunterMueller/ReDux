%%  comment symbol
%%  AUTHOR Reinhard Buendgen
%%  STATUS not complete
%%  DESCRIPTION finitely presented commutative Ring with unit
%%  DESCRIPTION <2,3,5; 2+2=1, 3+3+3=1, 5+5+5+5+5=1>
%%  ORIGIN R. Buendgen Term Cpmpletion vs. Algebraic Completion
DATATYPE R235;
SORT
	RING;
EXTERNAL
        SAC2IP: XREAD=IPRRDE, XWRITE=IPRWRE, XTERM=TFIPRE, XFG=IPRFG;
CONST
	0, 1, 2, 3, 5: RING;
VAR
	a, b, c, d, e: RING;
OPERATOR
	+: RING, RING -> RING;
	*: RING, RING -> RING;
	-: RING -> RING;
COERCION
        ': SAC2IP -> RING;
NOTATION
        ': ROUNDFIX ';    %% argument between quotes
PREC
        + < *;
THEORY
	+, *: AC;
PROPERTY
        -: SAC2IP=IPRNEG;
        *: SAC2IP=IPRPRO;
        +: SAC2IP=IPRSUM;
        1: SAC2IP=IPRONE;
        0: SAC2IP=PRZERO;
AXIOM
 [1] 0 + a == a;
 [2] 0 * a == 0;
 [3] 1 * a == a;
 [4] -0 == 0;
 [5] --a == a;
 [6] a + -a == 0;
 [7] -(a + b) == -a + -b;
 [8] a * -b == -(a * b);
 [9] a * (b + c) == a*b + a*c;
[10] 2 + 2 == 1;
[11] 3 + 3 + 3 == 1;
[12] 5 + 5 + 5 + 5 + 5 == 1;
END

